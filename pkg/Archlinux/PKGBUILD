# Maintainer: Jan Veen <jan.veen Ã¦t posteo.de>
pkgname=alerter
pkgver=2.0.1
pkgrel=2
pkgdesc="Send messages to a chat service"
arch=('x86_64')
url="https://gitlab.com/veenj/$pkgname"
license=('GPL3')
changelog=../../CHANGELOG.md
makedepends=('rust')
backup=("etc/$pkgname/$pkgname.yml"
        "etc/$pkgname/alert.yml"
        "etc/$pkgname/log4rs.yml")
depends=('systemd-libs')

build() {
    cd "$srcdir"
    cargo build --release
}

check() {
    cd "$srcdir"
    cargo fmt --all -- --check
    cargo clippy --release -j 8 -- -D clippy::all
    cargo test -j 1
}

package() {
    cd "$srcdir"

    install -D -m 755 target/release/$pkgname $pkgdir/usr/bin/$pkgname
    install -D -m 755 target/release/alert $pkgdir/usr/bin/alert
    install -D -m 644 pkg/$pkgname.yml $pkgdir/etc/$pkgname/$pkgname.yml
    install -D -m 644 pkg/alert.yml $pkgdir/etc/$pkgname/alert.yml
    install -D -m 644 pkg/log4rs.yml $pkgdir/etc/$pkgname/log4rs.yml
    install -D -m 644 pkg/systemd.service $pkgdir/usr/lib/systemd/system/$pkgname.service
    install -D -m 644 pkg/systemd.sysusers $pkgdir/usr/lib/sysusers.d/${pkgname}.conf
    install -D -m 644 pkg/systemd.tmpfiles $pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf
}
